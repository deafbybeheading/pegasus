<?xml version="1.0" encoding="utf-8"?>
<mx:List xmlns:mx="http://www.adobe.com/2006/mxml" width="400" height="300"
	   fontFamily="courier" fontSize="16">
    <mx:Script>
    	<![CDATA[
    		import mx.collections.ArrayCollection;

    		private var _dataSource:ArrayCollection = new ArrayCollection();

            public function set bytes(value:ByteArray):void {
            	_dataSource.removeAll();

            	var origPos:int = value.position;
            	value.position = 0;
            	var offset:int = 0;
            	while (value.bytesAvailable > 0) {
            		var row:Object = { offset: String('0000000' + offset.toString(16)).substr(-8, 8) };
            		var hexVals:Array = [];
            		var asciiVals:Array = [];
            		for (var j:int = 0; j < 16 && value.bytesAvailable > 0; j++) {
            			if (j == 8) {
            				hexVals.push(' ');
            			}
            			var currByte:int = value.readByte();
            			hexVals.push(('0' + currByte.toString(16)).substr(-2, 2));
            			asciiVals.push(currByte >= 32 && currByte <= 127 ? String.fromCharCode(currByte) : '.');
            		}
            		offset += 16;
            		row.hex = hexVals.join(' ');
            		row.ascii = '|' + asciiVals.join('') + '|';
            		_dataSource.addItem(row);
            	}
                dataProvider = _dataSource;
            }
    	]]>
    </mx:Script>
    <mx:itemRenderer>
    	<mx:Component>
    		<mx:HBox height="20">
    			<mx:Label text="{data.offset}"/>
                <mx:Label text="{data.hex}"/>
                <mx:Label text="{data.ascii}"/>
    		</mx:HBox>
    	</mx:Component>
    </mx:itemRenderer>
</mx:List>
